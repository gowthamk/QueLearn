(declare-datatypes () ((Eff (ea0) (ea1) (ea2) (eb0) (eb1))))
(declare-sort Id)
(declare-sort Stringe)
(declare-sort Fn)
(declare-datatypes () ((Oper (NT (userId Id) (content Stringe)) (GT) (NTTL (tweetIdA Id)) (GTTL) (NTUL (tweetIdB Id)) (GTUL) (NOP))))
(declare-fun in_S (Eff) Bool)
(declare-fun so (Eff Eff) Bool)
(declare-fun hb (Eff Eff) Bool)
(declare-fun vis (Eff Eff) Bool)
(declare-fun objid (Eff) Id)
(declare-fun sameobj (Eff Eff) Bool)
(declare-fun none_stringe () Stringe)
(declare-fun oper (Eff) Oper)
(declare-fun fn (Eff) Fn)
(assert (forall ((x Eff))
    (= (not (in_S x)) (or (= x ea0) (= x ea1) (= x ea2) (= x eb0) (= x eb1)))))
(assert (forall ((a Eff) (b Eff) (c Eff)) 
    (=> (and (so a b) (so b c)) (so a c))))
(assert (forall ((a Eff) (b Eff)) 
    (=> (or (vis a b) (so a b)) (hb a b))))
(assert (forall ((a Eff) (b Eff) (c Eff)) 
    (=> (and (hb a b) (hb b c)) (hb a c))))
(assert (forall ((e Eff)) (not (hb e e))))
(assert (forall ((a Eff) (b Eff)) 
    (= (sameobj a b) (= (objid a) (objid b)))))
; vis => sameobj
(assert (forall ((a Eff)(b Eff)) 
  (=> (vis a b) (sameobj a b))))
(assert (forall ((x Eff) (y Eff)) 
    (=> (in_S y) (not (or (vis x y) (so x y))))))
; eb0 and eb1 are GTUL and GT witness operations.
(assert (= (oper eb0) GTUL))
(assert (= (oper eb1) GT))
; They are in so
(assert (so eb0 eb1))
; Their function is GET_USERLINE
(declare-const GET_USERLINE Fn)
(assert (= (fn eb0) GET_USERLINE))
(assert (= (fn eb1) GET_USERLINE))
; Define getTweet1
(declare-fun getTweet1 (Eff Id) Stringe)
; Define getTweetsInUL1
(declare-fun getTweetsInUL1 (Eff Id Id) Bool)
; Assert invariant in pre-state
(assert (forall ((uid Id)(tid Id)) 
    (=> (getTweetsInUL1 eb0 uid tid)
        (not (= (getTweet1 eb1 tid) none_stringe)))))
; Execute newTweet txn
(declare-const ADD_NEW_TWEET Fn)
(declare-const uid0 Id)
(declare-const stringe0 Stringe)
(declare-const tid0 Id)
(assert (= (oper ea0) (NT uid0 stringe0)))
(assert (= (objid ea0) tid0))
(assert (= (fn ea0) ADD_NEW_TWEET))
(declare-const ulid0 Id)
(assert (= (oper ea1) (NTUL tid0)))
(assert (= (objid ea1) ulid0))
(assert (= (fn ea1) ADD_NEW_TWEET))
(declare-const tlid0 Id)
(assert (= (oper ea2) (NTTL tid0)))
(assert (= (objid ea2) tlid0))
(assert (= (fn ea2) ADD_NEW_TWEET))
(assert (and (so ea0 ea1) (so ea1 ea2)))
; Define getTweet2
(declare-fun getTweet2 (Eff Id) Stringe)
(assert (forall ((a Eff) (b Id))
  (ite (and (vis ea0 a) (is-NT (oper ea0)) (= (objid ea0) b))
       (= (getTweet2 a b) (content (oper ea0)))
       (ite (and (vis ea1 a) (is-NT (oper ea1)) (= (objid ea1) b))
            (= (getTweet2 a b) (content (oper ea1)))
            (ite (and (vis ea2 a) (is-NT (oper ea2)) (= (objid ea2) b))
                 (= (getTweet2 a b) (content (oper ea2)))
                 (= (getTweet2 a b) (getTweet1 a b)))))))
; Define getTweetsInUL2
(declare-fun getTweetsInUL2 (Eff Id Id) Bool)
(assert (forall ((a Eff) (b Id) (c Id)) 
    (= (getTweetsInUL2 a b c)
       (or (and (vis ea0 a) (is-NTUL (oper ea0)) 
                (= (objid ea0) b) (= (tweetIdB (oper ea0)) c))
           (and (vis ea1 a) (is-NTUL (oper ea1)) 
                (= (objid ea1) b) (= (tweetIdB (oper ea1)) c))     
           (and (vis ea2 a) (is-NTUL (oper ea2)) 
                (= (objid ea2) b) (= (tweetIdB (oper ea2)) c))
           (getTweetsInUL1 a b c)))))
(declare-const Inv Bool)
(assert (= Inv (forall ((uid Id)(tid Id)) 
    (=> (getTweetsInUL2 eb0 uid tid)
        (not (= (getTweet2 eb1 tid) none_stringe))))))
(assert (not Inv))
(check-sat)
(get-model)
